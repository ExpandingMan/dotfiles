# LCARS prompt with dracula colors

# Dependencies
pmodload 'helper'

function prompt_drac_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_drac_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_drac_precmd

  # %f to turn color off
  __drac_background="%F{#282a36}"
  __drac_selection="%F{#44475a}"
  __drac_foreground="%F{#f8f8f2}"
  __drac_comment="%F{#6272a4}"
  __drac_cyan="%F{#8be9fd}"
  __drac_green="%F{#50fa7b}"
  __drac_orange="%F{#ffb86c}"
  __drac_pink="%F{#ff79c6}"
  __drac_purple="%F{#bd93f9}"
  __drac_red="%F{#ff5555}"
  __drac_yellow="%F{#f1fa8c}"

  __nocolor="$__drac_foreground"

  # %k to turn color off
  __drac_back_background="%K{#282a36}"
  __drac_back_selection="%K{#44475a}"
  __drac_back_comment="%K{#6272a4}"
  __drac_back_cyan="%K{#8be9fd}"
  __drac_back_green="%K{#50fa7b}"
  __drac_back_orange="%K{#ffb86c}"
  __drac_back_pink="%K{#ff79c6}"
  __drac_back_purple="%K{#bd93f9}"
  __drac_back_red="%K{#ff5555}"
  __drac_back_yellow="%K{#f1fa8c}"

  # Set editor-info params, TODO these aren't working right now, see zprezto #1789
  zstyle ':prezto:module:editor:info:completing' format '$__drac_red...$__nocolor'
  zstyle ':prezto:module:editor:info:keymap:primary' format "${__drac_comment}◗$__nocolor"
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '♺'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '⍟'

  # Set get-info parameters
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format "${__drac_purple}∙%s$__nocolor"
  zstyle ':prezto:module:git:info:added' format "${__drac_yellow}✚$__nocolor"
  zstyle ':prezto:module:git:info:ahead' format "${__drac_red}⚡$__nocolor"
  zstyle ':prezto:module:git:info:behind' format "${__drac_red}↯$__nocolor"
  zstyle ':prezto:module:git:info:branch' format "${__drac_pink}∙%b$__nocolor"
  zstyle ':prezto:module:git:info:clean' format "${__drac_green}✔$__nocolor"
  zstyle ':prezto:module:git:info:commit' format "${__drac_cyan}%.7c$__nocolor"
  zstyle ':prezto:module:git:info:deleted' format "${__drac_red}✖$__nocolor"
  zstyle ':prezto:module:git:info:modified' format "${__drac_yellow}✱$__nocolor"
  zstyle ':prezto:module:git:info:renamed' format "${__drac_orange}➜$__nocolor"
  zstyle ':prezto:module:git:info:stashed' format "${__drac_orange}✭$__nocolor"
  zstyle ':prezto:module:git:info:unmerged' format "${__drac_red}═$__nocolor"
  zstyle ':prezto:module:git:info:untracked' format "${__drac_orange}▪$__nocolor"
  zstyle ':prezto:module:git:info:keys' format \
    'rprompt' "${__drac_purple}◖▯%c%b%s$__drac_pink∙$__nocolor%C%A%B%S%a%d%m%r%U%u"

  # Get the time
  local time="%T"

  # TODO experimenting because of weird problems on remote
  # Remote hosts change colors
  #if [[ -n "$SSH_CLIENT"  ||  -n "$SSH2_CLIENT" ]]; then
  #  local host="${__drac_orange}%m$__nocolor" #SSH
  #else
  #  local host="${__drac_yellow}%m$__nocolor" # no SSH
  #fi
  local host="${__drac_yellow}%m$__nocolor"

  local user="%(!.${__drac_red}.${__drac_yellow})%n"

  local retval="%(?.${__drac_cyan}%?.${__drac_red}%?)$__nocolor"

  # Define prompts.
  #SPROMPT='zsh: correct ${_prompt_drac_colors[6]}%R${_prompt_drac_nocolor} to ${_prompt_drac_colors[4]}%r${_prompt_drac_nocolor} [nyae]? '
  PROMPT="$__drac_comment◖$__nocolor $retval %k$user$__drac_comment@$host ${__drac_comment}▮%k $__drac_purple%/ $__drac_comment▮◗$__nocolor "
  RPROMPT='${git_info[rprompt]}$__nocolor'
}

prompt_drac_setup "$@"
