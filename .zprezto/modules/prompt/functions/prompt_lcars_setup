# LCARS prompt with dracula colors
#
# to use this do:
# `ln -s ~/dotfiles/.zprezto/modules/prompt/functions/prompt_lcars_setup ~/.zprezto/modules/prompt/functions/`

# Dependencies
pmodload 'helper'

function prompt_drac_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_drac_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_drac_precmd

  zstyle ':prezto:module:prompt' managed 'yes'

  # NOTE: these use defaults when possible to improve compatibility
  # set colors in terminal config
  # %f to turn color off
  __drac_background="%F{#282A36}"
  __drac_selection="%F{#44475A}"
  __drac_foreground="%F{white}"
  __drac_comment="%F{#6272A4}"
  __drac_cyan="%F{cyan}"
  __drac_green="%F{green}"
  __drac_orange="%F{#ffB86C}"
  __drac_pink="%F{magenta}"
  # for some reason this picks the purple
  __drac_purple="%F{blue}"
  __drac_red="%F{red}"
  __drac_yellow="%F{yellow}"

  __nocolor="$__drac_foreground"

  # %k to turn color off
  __drac_back_background="%K{#282A36}"
  __drac_back_selection="%K{#44475A}"
  __drac_back_comment="%K{#6272A4}"
  __drac_back_cyan="%K{cyan}"
  __drac_back_green="%K{green}"
  __drac_back_orange="%K{#ffB86C}"
  __drac_back_pink="%K{magenta}"
  __drac_back_purple="%K{blue}"
  __drac_back_red="%K{red}"
  __drac_back_yellow="%K{yellow}"

  # Set editor-info params
  zstyle ':prezto:module:editor:info:completing' format "$__drac_red◖⋯$__nocolor "
  zstyle ':prezto:module:editor:info:keymap:primary' format "${__drac_green}▮${__drac_purple}◗$__nocolor "
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format "${__drac_red}▮${__drac_purple}◗$__nocolor "
  zstyle ':prezto:module:editor:info:keymap:alternate' format "${__drac_orange}▯${__drac_purple}◗$__nocolor "

  # Set get-info parameters
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format "${__drac_purple}∙%s$__nocolor"
  zstyle ':prezto:module:git:info:added' format "${__drac_yellow}✚$__nocolor"
  zstyle ':prezto:module:git:info:ahead' format "${__drac_red}⚡$__nocolor"
  zstyle ':prezto:module:git:info:behind' format "${__drac_red}↯$__nocolor"
  zstyle ':prezto:module:git:info:branch' format "${__drac_pink}∙%b$__nocolor"
  zstyle ':prezto:module:git:info:clean' format "${__drac_green}✔$__nocolor"
  zstyle ':prezto:module:git:info:commit' format "${__drac_cyan}%.7c$__nocolor"
  zstyle ':prezto:module:git:info:deleted' format "${__drac_red}✖$__nocolor"
  zstyle ':prezto:module:git:info:modified' format "${__drac_yellow}✱$__nocolor"
  zstyle ':prezto:module:git:info:renamed' format "${__drac_orange}➜$__nocolor"
  zstyle ':prezto:module:git:info:stashed' format "${__drac_orange}✭$__nocolor"
  zstyle ':prezto:module:git:info:unmerged' format "${__drac_red}═$__nocolor"
  zstyle ':prezto:module:git:info:untracked' format "${__drac_orange}▪$__nocolor"
  zstyle ':prezto:module:git:info:keys' format \
    'rprompt' "${__drac_purple}◖▯%c%b%s$__drac_pink∙$__nocolor%C%A%B%S%a%d%m%r%U%u"

  # Get the time
  local time="%T"

  # Remote hosts change colors
  if [[ -n "$SSH_CLIENT"  ||  -n "$SSH2_CLIENT" ]]; then
    local host="${__drac_pink}%m$__nocolor" #SSH
  else
    local host="${__drac_yellow}%m$__nocolor" # no SSH
  fi

  local user="%(!.${__drac_red}.${__drac_yellow})%n"

  local retval="%(?.${__drac_cyan}%?.${__drac_red}%?)$__nocolor"

  # Define prompts.
  SPROMPT='zsh: correct ${__drac_pink}%R${__nocolor} to ${__drac_green}%r${__nocolor} [nyae]? '
  PROMPT="$__drac_purple◖$__nocolor$retval$__drac_purple▮$user$__drac_purple@$host${__drac_purple}▮$__drac_cyan%/"'${editor_info[keymap]}'
  RPROMPT='${git_info[rprompt]}$__nocolor'
}

prompt_drac_setup "$@"
